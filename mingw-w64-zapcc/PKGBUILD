# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Zion Nimchuk <zionnimchuk@gmail.com>
# Contributor: Mateusz Miku≈Ça <mati865@gmail.com

_realname=zapcc
pkgbase=mingw-w64-${_realname}-git
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
pkgver=57.9bd04e761
pkgrel=1
pkgdesc="a caching C++ compiler based on clang, designed to perform faster compilations"
arch=('x86_64')
url="https://www.zapcc.com/"
license=('NCSA')
depends=("${MINGW_PACKAGE_PREFIX}-gcc")
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake>=3.4.3"
             "${MINGW_PACKAGE_PREFIX}-z3"
             "${MINGW_PACKAGE_PREFIX}-libffi"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-python3-sphinx"
             "${MINGW_PACKAGE_PREFIX}-python2"
             "${MINGW_PACKAGE_PREFIX}-swig"
             'tar'
             'groff'
			 'git')
options=('!debug' 'strip')
source=("git+https://github.com/yrnkrn/zapcc.git")

sha256sums=('SKIP')

pkgver() {
	cd "${srcdir}"/${_realname}
	printf "%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {

	[[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
	mkdir -p "${srcdir}/build-${MINGW_CHOST}" && cd "${srcdir}/build-${MINGW_CHOST}"
	
	cmake -G 'MSYS Makefiles' \
	 -DCMAKE_BUILD_TYPE=Release \
	 -DLLVM_ENABLE_WARNINGS=OFF \
	 -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}"  \
	 -DLLVM_DEFAULT_TARGET_TRIPLE=$CARCH-pc-windows-gnu  \
	 ../${_realname}

	 make --quiet
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install
}
